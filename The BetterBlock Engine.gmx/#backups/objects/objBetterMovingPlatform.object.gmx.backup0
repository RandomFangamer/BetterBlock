// 2020-12-20 04:05:57
#event properties (no comments/etc. here are saved)
parent_index = objPlatform;
sprite_index = sprMovingPlatform;
uses_physics = false;

#event create
yspeed = 0;
player = noone;
wasCarrying = false;
image_speed = 0;
bbox_tPrevious = bbox_top;
bbox_bPrevious = bbox_bottom;

#event other_user0
player = instance_place(x, y-2*global.grav, objPlayer);
if (wasCarrying && player == noone) player = instance_place(xprevious, yprevious-2*global.grav, objPlayer); //prev. frame

if (player == noone && global.grav == 1 && wasCarrying){
    player = instance_place(xprevious, yprevious-2*global.grav - max(bbox_top - bbox_tPrevious, 0), objPlayer);
}
if (player == noone && global.grav == -1 && wasCarrying){
    player = instance_place(xprevious, yprevious-2*global.grav - min(bbox_bottom - bbox_bPrevious, 0), objPlayer); //prev. frame position and scale
}

//Not on top of platform
if (player != noone && !((global.grav == 1 && player.yprevious + player.bottomToOrigin - player.gravity <= bbox_tPrevious) || (global.grav == -1 && player.yprevious - player.topToOrigin + other.gravity >= bbox_bPrevious)))
    player = noone;

//Move Player
if (player != noone && (!instance_exists(player.riding) || player.riding == id || scrIsBetterBlock(player.riding) || (sign(y - yprevious) == -global.grav && ((global.grav == 1 && bbox_top < player.riding.bbox_top) || (global.grav == -1  && bbox_bottom > player.riding.bbox_bottom))))){
    with (player){
        riding = other;
        djump = 1;
        onPlatform = true;
        
        //Vertical Movement
        if (global.grav == 1){
            if (vspeed > 0) vspeed = 0;
            platCheck = other.bbox_top - (y+bottomToOrigin);
            for(platCounter=1; platCounter<=ceil(abs(platCheck)); platCounter++){
                if (place_meeting(x, y+platCounter*sign(platCheck), objBlock)){ //touching objBlock or a child of objBlock
                    with (objBlock){
                        if (place_meeting(x, y-other.platCounter*sign(other.platCheck), other) && id != other.riding){ //avoid being pulled into a block or through a platform
                            if (other.platCheck > 0 && bbox_top >= other.y + other.bottomToOrigin - 1.1*other.gravity){
                                other.y = bbox_top - other.bottomToOrigin; //stand on the block
                                other.platCheck = 0; //end the for loop
                                other.onPlatform = (object_is_ancestor(object_index, objPlatform) || object_index == objPlatform);
                                other.riding = id;
                                break;
                            }else if (scrIsBlock() && other.platCheck < 0 && (bbox_bottom + 1) <= other.y - other.topToOrigin){ //get pushed through platform
                                other.vspeed = 0;
                                other.y = (bbox_bottom + 1) + other.topToOrigin; //snap to the bottom of the block
                                other.platCheck = 0; //end the for loop
                                break;
                            }
                        }
                    }
                }
            }
            y += platCheck;
        }else if (global.grav == -1){
            if (vspeed < 0) vspeed = 0;
            platCheck = (other.bbox_bottom + 1) - (y-topToOrigin);
            for(platCounter=1; platCounter<=ceil(abs(platCheck)); platCounter++){
                if (place_meeting(x, y+platCounter*sign(platCheck), objBlock)){ //touching objBlock or a child of objBlock
                    with (objBlock){
                        if (place_meeting(x, y-other.platCounter*sign(other.platCheck), other) && id != other.riding){ //avoid being pulled into a block or through a platform
                            if (other.platCheck < 0 && (bbox_bottom + 1) <= other.y + other.topToOrigin - 1.1*other.gravity){
                                other.y = (bbox_bottom + 1) + other.topToOrigin; //stand on the block
                                other.platCheck = 0 //end the for loop
                                other.onPlatform = (object_is_ancestor(object_index, objPlatform) || object_index == objPlatform);
                                other.riding = id;
                                break;
                            }else if (scrIsBlock() && other.platCheck > 0 && bbox_top >= other.y - other.bottomToOrigin){ //get pushed through platform
                                other.vspeed = 0;
                                other.y = bbox_top - other.bottomToOrigin; //snap to the top of block
                                other.platCheck = 0; //end the for loop
                                break;
                            }
                        }
                    }
                }
            }
            y += platCheck;
        }
        //Horizontal Movement
        if (riding = other.id){ //will not be true if the player was placed on a block while calculating vertical movement in the above section;
            platCheck = other.x - other.xprevious;
            platCheck -= hPush; //prevents the player from hanging on the edge of a moving platfom when they should have fallen
            for(platCounter=1; platCounter<=ceil(abs(platCheck)); platCounter++){
                if (place_meeting(x+platCounter*sign(platCheck), y, objBlock)){
                    with (objBlock){
                        if (scrIsBlock() && place_meeting(x-other.platCounter*sign(other.platCheck), y, other)){
                            other.platCheck = (other.platCounter-1)*sign(other.platCheck);
                            other.platCounter = ceil(abs(other.platCheck));
                            break;
                        }
                    }
                }
            }
            x += platCheck;
        }
    }
}

wasCarrying = !(player == noone);

bbox_tPrevious = bbox_top;
bbox_bPrevious = bbox_bottom;